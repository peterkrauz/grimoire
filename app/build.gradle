plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'

    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
}

android {
    compileSdkVersion AndroidConfig.compileSdkVersion
    buildToolsVersion AndroidConfig.buildToolsVersion

    defaultConfig {
        applicationId AndroidConfig.applicationId
        minSdkVersion AndroidConfig.minSdkVersion
        targetSdkVersion AndroidConfig.targetSdkVersion
        versionCode AndroidConfig.versionCode
        versionName AndroidConfig.versionName
        testInstrumentationRunner AndroidConfig.testInstrumentationRunner
        ext.betaDistributionGroupAliases = AndroidConfig.firebaseAppTestersGroupAlias

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Grimoire"
            firebaseAppDistribution {
                releaseNotesFile="releasenotes.txt"
            }
        }
        beta {
            initWith debug
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            resValue "string", "app_name", "Grimoire Beta"
            firebaseAppDistribution {
                releaseNotesFile="releasenotes.txt"
                groups=AndroidConfig.firebaseAppTestersGroupAlias
            }
        }
    }

    compileOptions {
        sourceCompatibility = AndroidConfig.javaVersion
        targetCompatibility = AndroidConfig.javaVersion
    }
    kotlinOptions {
        jvmTarget = AndroidConfig.jvmTarget
    }
}

dependencies {
//    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation Kotlin.standardLibrary
    implementation Android.coreKtx
    implementation Android.appCompat
    implementation Android.constraintLayout
    implementation Material.material
    implementation Navigation.navigationUi
    implementation Navigation.navigationFragment

    implementation Firebase.analytics

    implementation project(ModulePaths.common)
    implementation project(ModulePaths.home)
    implementation project(ModulePaths.notes)
    implementation project(ModulePaths.characters)
}